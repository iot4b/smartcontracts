pragma ever-solidity ^0.70.0;

pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";

import {IUpgradable} from "./interfaces/IUpgradable.tsol";
import {Errors} from "./libraries/Errors.tsol";
import {INode} from "./interfaces/INode.tsol";
import {DeviceGroup} from "./DeviceGroup.tsol";
import {Elector} from "./Elector.tsol";

contract Device is IUpgradable {
    string name_; // device name
    mapping(uint256 => address) owners_; // device owners: public_key => contract_address(if exists)
    string dtype_; // device type
    string version_; // firmware version
    uint32 lastRegisterTime_; // last registration timestamp

    address group_; // address of DeviceGroup contract that device belongs to

    address node_; // current node address

    address elector_; // elector contract address

    address deviceAPI_; // device API contract address

    address vendor_; // vendor contract address
    string vendorData_; // vendor data

    bool lock_; // device lock / unlock for management
    bool stat_; // enable / disable collecting stat
    bool events_; // enable / disable sending events

    //events

    // Event that is emitted when node address is changed
    event NodeChange(address contractAddress, address oldNode, address newNode);

    // Event that is emitted when some device data was changed
    event DeviceUpdate(address contractAddress, address newNode);

    // Custom event
    event DeviceEvent(string name, string data);

    function deviceEvent(string name, string data) external view onlyOwner {
        emit DeviceEvent(name, data);
        DeviceGroup(group_).groupEvent(name, data);
    }

    modifier onlyDevice() {
        require(msg.pubkey() == tvm.pubkey(), Errors.NOT_ALLOWED);
        tvm.accept();
        _ensureGas();
        _;
    }

    modifier onlyOwner() {
        require(msg.pubkey() == tvm.pubkey() || owners_.exists(msg.pubkey()), Errors.NOT_ALLOWED);
        tvm.accept();
        _ensureGas();
        _;
    }

    constructor(
        string name,
        address elector,
        address vendor,
        address group,
        address deviceAPI,
        mapping(uint256 => address) owners,
        string dtype,
        string version,
        string vendorData,
        bool stat,
        bool events
    ) {
        tvm.accept();

        // setup contract data
        name_ = name;
        elector_ = elector;
        vendor_ = vendor;
        group_ = group;
        deviceAPI_ = deviceAPI;
        owners_ = owners;
        dtype_ = dtype;
        version_ = version;
        vendorData_ = vendorData;
        stat_ = stat;
        events_ = events;
    }

    function get() public view returns (
        string name,
        address node,
        address elector,
        address vendor,
        address group,
        address deviceAPI,
        mapping(uint256 => address) owners,
        bool lock,
        bool stat,
        bool events,
        string dtype,
        string version,
        string vendorData,
        uint32 lastRegisterTime
    ) {
        return (
            name_,
            node_,
            elector_,
            vendor_,
            group_,
            deviceAPI_,
            owners_,
            lock_,
            stat_,
            events_,
            dtype_,
            version_,
            vendorData_,
            lastRegisterTime_
        );
    }

    // get tokens for gas from device group if needed
    function _ensureGas() private view {
        if (address(this).balance < 0.5 ever) {
            require(group_ != address(0), Errors.EMPTY_GROUP);
            DeviceGroup(group_).transfer(address(this), 1 ever);
        }
    }

    // Send update event to node
    function _sendUpdate() private view {
        INode(node_).deviceUpdate{
            value: 0.1 ever,
            flag: MsgFlag.REMAINING_GAS
        }(address(this));
    }

    // Set current node address for device
    function setNode(address node) external onlyDevice {
        // check if node address is not empty
        require(node != address(0), Errors.EMPTY_ADDRESS);

        Elector(elector_).getPayment{
            value: 0.1 ever,
            flag: MsgFlag.REMAINING_GAS
        }(group_);

        INode(node).deviceRegister{
            value: 0.1 ever,
            flag: MsgFlag.REMAINING_GAS
        }(address(this), node_);

        //TvmCell message = tvm.encodeBody(INode.deviceRegister, address(this), node_);
        //node.transfer(0, false, 3, message);

        emit NodeChange(address(this), node_, node);

        // set new node address
        node_ = node;
        // update last register time
        lastRegisterTime_ = block.timestamp;
    }

    // Add owner public key and contract address if any
    function addOwner(uint256 pubKey, address addr) external onlyOwner {
        owners_[pubKey] = addr;
        _sendUpdate();
    }

    // Remove owner data by public key from owners list
    function removeOwner(uint256 pubKey) external onlyOwner {
        delete owners_[pubKey];
        _sendUpdate();
    }

    // Set device name
    function setName(string name) external onlyOwner {
        name_ = name;
        _sendUpdate();
    }

    // lock / unlock device
    function setLock(bool lock) external onlyOwner {
        lock_ = lock;
        _sendUpdate();
    }

    // enable / disable collecting stat
    function setStat(bool stat) external onlyOwner {
        stat_ = stat;
        _sendUpdate();
    }

    // enable / disable sending events
    function setEvents(bool events) external onlyOwner {
        events_ = events;
        _sendUpdate();
    }

    // set device group contract address
    function setGroup(address group) external onlyOwner {
        group_ = group;
        _sendUpdate();
    }

    // set device API contract address
    function setDeviceAPI(address deviceAPI) external onlyOwner {
        deviceAPI_ = deviceAPI;
        _sendUpdate();
    }

    // set vendor contract address
    function setVendor(address vendor) external onlyOwner {
        vendor_ = vendor;
        _sendUpdate();
    }

    // send tokens to dest address
    function sendTransaction(address dest, uint128 value) external view onlyOwner {
        dest.transfer(value, true, 3);
    }

    // Upgrade contract code
    function upgrade(TvmCell code) external override onlyOwner {
        TvmCell data = abi.encode(
            name_,
            node_,
            elector_,
            vendor_,
            group_,
            deviceAPI_,
            owners_,
            lock_,
            stat_,
            events_,
            dtype_,
            version_,
            vendorData_,
            lastRegisterTime_);

        tvm.setcode(code);
        tvm.setCurrentCode(code);

        onCodeUpgrade(data);
    }

    function onCodeUpgrade(TvmCell data) private {
//        tvm.resetStorage();
//
//        (name_,
//        node_,
//        elector_,
//        vendor_,
//        group_,
//        owners_,
//        lock_,
//        stat_,
//        events_,
//        dtype_,
//        version_,
//        vendorData_,
//        lastRegisterTime_) = abi.decode(data,
//            (string,
//            address,
//            address,
//            address,
//            address,
//            mapping(uint256 => address),
//            bool,
//            bool,
//            bool,
//            string,
//            string,
//            string,
//            uint32)
//        );
    }
}
